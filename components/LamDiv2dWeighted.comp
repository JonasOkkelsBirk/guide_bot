/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright (C) 1997-2010, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Component: LamDiv2dWeighted
*
* %I
* Modified by: Kaspar Hewitt KlenÃ¸ from L_monitor (1.12b), Oct 2010.
*
* Wavelength-sensitive monitor with divergence restrictions.
*
* %D
* A square single monitor that measures the wavelength of the incoming
* neutrons.
*
* Example: L_monitor(xmin=-0.1, xmax=0.1, ymin=-0.1, ymax=0.1,
*           nchan=20, filename="Output.L", Lmin=2, Lmax=10, h_maxdiv=2, v_maxdiv=2)
*
* %P
* INPUT PARAMETERS:
*
* xmin:     Lower x bound of detector opening (m)
* xmax:     Upper x bound of detector opening (m)
* ymin:     Lower y bound of detector opening (m)
* ymax:     Upper y bound of detector opening (m)
* xwidth:   Width/diameter of detector (x). Overrides xmin,xmax. (m)
* yheight:  Height of detector (y). Overrides ymin,ymax. (m)
* Lmin:     Minimum wavelength to detect (AA)
* Lmax:     Maximum wavelength to detect (AA)
* h_maxdiv  Maximum horizontal divergence to accept (+-)
* v_maxdiv  Maximum vertical divergence to accept (+-)
* exponent  Weigth rays with lambda^n. Zero for no weighting.
* nchan:    Number of wavelength channels (1)
* filename: Name of file in which to store the detector image (text)
* restore_neutron: If set, the monitor does not influence the neutron state (1)
*
* OUTPUT PARAMETERS:
*
* L_N:      Array of neutron counts
* L_p:      Array of neutron weight counts
* L_p2:     Array of second moments
*
* %E
*******************************************************************************/

DEFINE COMPONENT LamDiv2dWeighted
DEFINITION PARAMETERS (nchan=20, string filename, restore_neutron=0)
SETTING PARAMETERS (xmin=0, xmax=0, ymin=0, ymax=0, xwidth=0, yheight=0, Lmin, Lmax, h_maxdiv=2, v_maxdiv=2,exponent=0)
OUTPUT PARAMETERS (L_N, L_p, L_p2)
//POLARISATION PARAMETERS (sx,sy,sz)
DECLARE
  %{
    double L_N[nchan];
    double L_p[nchan], L_p2[nchan];
  %}
INITIALIZE
  %{
    int i;

    if (xwidth  > 0) { xmax = xwidth/2;  xmin = -xmax; }
    if (yheight > 0) { ymax = yheight/2; ymin = -ymax; }

    if ((xmin >= xmax) || (ymin >= ymax)) {
            printf("L_monitor: %s: Null detection area !\n"
                   "ERROR      (xwidth,yheight,xmin,xmax,ymin,ymax). Exiting",
           NAME_CURRENT_COMP);
      exit(0);
    }

    for (i=0; i<nchan; i++)
    {
      L_N[i] = 0;
      L_p[i] = 0;
      L_p2[i] = 0;
    }
  %}
TRACE
  %{
    int i;
    double h_div, v_div, L;

    PROP_Z0;
    if (x>xmin && x<xmax && y>ymin && y<ymax)
    {
      h_div = RAD2DEG*atan2(vx,vz);
      v_div = RAD2DEG*atan2(vy,vz);
 
   if (h_div < h_maxdiv && h_div > -h_maxdiv &&
          v_div < v_maxdiv && v_div > -v_maxdiv)
     {
      L = (2*PI/V2K)/sqrt(vx*vx + vy*vy + vz*vz);
      i = floor((L-Lmin)*nchan/(Lmax-Lmin));
      if(i >= 0 && i < nchan)
      {
        L_N[i]++;
        L_p[i] += p*pow(L,exponent);
        L_p2[i] += p*p*pow(L,exponent)*pow(L,exponent);
        SCATTER;
      }
     }
    } 
    if (restore_neutron) {
      RESTORE_NEUTRON(INDEX_CURRENT_COMP, x, y, z, vx, vy, vz, t, sx, sy, sz, p);
    }
  %}
SAVE
  %{
    DETECTOR_OUT_1D(
        "Wavelength monitor",
        "Wavelength [AA]",
        "Intensity",
        "L", Lmin, Lmax, nchan,
        &L_N[0],&L_p[0],&L_p2[0],
        filename);
  %}

MCDISPLAY
%{
  magnify("xy");
  multiline(5, (double)xmin, (double)ymin, 0.0,
               (double)xmax, (double)ymin, 0.0,
               (double)xmax, (double)ymax, 0.0,
               (double)xmin, (double)ymax, 0.0,
               (double)xmin, (double)ymin, 0.0);
%}

END
